// gcServer

plugins {
	id 'java'
	id 'application'
	id 'eclipse'
	id 'com.github.johnrengelman.shadow' version '1.2.2'
}

version = '3.6.1'
group = 'com.growcontrol.server'
mainClassName = 'com.growcontrol.server.gcServer'
defaultTasks 'clean', 'build', 'shadow'

def junitVersion = '4.12'

[ compileJava, compileTestJava ]*.options*.encoding = 'UTF-8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
	main {
		java {
			srcDir 'src'
		}
		resources {
			srcDir 'resources'
		}
	}
}

jar {
	archiveName = "${project.name}-${project.version}-sparse.jar"
	// write app.properties file
	Properties props = new Properties()
	File propsFile = new File('resources/app.properties')
	props.load(propsFile.newDataInputStream())
	props.setProperty('name',       "${project.name}")
	props.setProperty('title',      'GrowControl-Server')
	props.setProperty('version',    "${project.version}")
	props.setProperty('url',        'http://growcontrol.com')
	props.setProperty('org_name',   'PoiXson')
	props.setProperty('org_url',    'http://poixson.com')
	props.setProperty('issue_name', 'github')
	props.setProperty('issue_url',  'https://github.com/PoiXson/GrowControl/issues')
	props.store(propsFile.newWriter(), null)
}

shadowJar {
	archiveName = "${project.name}-${project.version}.jar"
}

repositories {
	jcenter()
}

dependencies {
	compile fileTree( dir: '../../output/', include: ["gcCommon-${project.version}.jar"] )
	compile fileTree( dir: '../../output/', include: ["gcServerAPI-${project.version}.jar"] )
	testCompile "junit:junit:${junitVersion}"
}

configurations.all {
	resolutionStrategy {
		failOnVersionConflict()
		cacheDynamicVersionsFor 5*60, 'seconds'
		cacheChangingModulesFor 0, 'seconds'
	}
}

task sourceJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives sourceJar
}

task copy (type: Copy) {
	def path = new File('../../output/')
	path.mkdirs()
	copy {
		from "build/libs/${project.name}-${project.version}.jar"
		into '../../output/'
	}
	copy {
		from "build/libs/${project.name}-${project.version}-sources.jar"
		into '../../output/'
	}
}
